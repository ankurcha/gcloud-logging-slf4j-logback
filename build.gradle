buildscript {
    repositories {
      mavenCentral()
    }
  dependencies {
      classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
  }
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'joda-time:joda-time:2.9.9'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'ch.qos.logback.contrib:logback-json-core:0.1.5'
    compile 'com.google.cloud:google-cloud-logging:1.2.0'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// handle release
if (project.hasProperty('release')) {
  apply plugin: 'maven'
  apply plugin: 'signing'
  apply plugin: 'io.codearte.nexus-staging'

  signing {
      sign configurations.archives
  }
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'gcloud-logging-slf4j-logback'
          description 'gcloud-logging json log appender for sl4j + logback'
          packaging 'jar'
          url 'https://github.com/ankurcha/gcloud-logging-slf4j-logback'

          scm {
            connection 'scm:git@github.com:ankurcha/gcloud-logging-slf4j-logback.git'
            developerConnection 'scm:git@github.com:ankurcha/gcloud-logging-slf4j-logback.git'
            url 'scm:git@github.com:ankurcha/gcloud-logging-slf4j-logback.git'
          }

          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id 'ankurcha'
              name 'Ankur Chauhan'
              email 'ankur@malloc64.com'
            }
          }
        }
      }
    }
  }

  nexusStaging {
      username = ossrhUsername
      password = ossrhPassword
  }
}
